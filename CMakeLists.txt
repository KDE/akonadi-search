# SPDX-License-Identifier: CC0-1.0
# SPDX-FileCopyrightText: none
cmake_minimum_required(VERSION 3.16)

set(PIM_VERSION "6.6.40")
project(AkonadiSearch VERSION ${PIM_VERSION})

# ECM setup
set(KF_MIN_VERSION "6.20.0")

set(CMAKE_CXX_SCAN_FOR_MODULES OFF)
find_package(ECM ${KF_MIN_VERSION} CONFIG REQUIRED)
set(CMAKE_MODULE_PATH ${ECM_MODULE_PATH})

include(GenerateExportHeader)
include(ECMGenerateHeaders)

include(CMakeDependentOption)
include(ECMSetupVersion)
file(
    GLOB_RECURSE ALL_CLANG_FORMAT_SOURCE_FILES
    agent/*.cpp
    agent/*.h
    agent/*.c
    akonadiplugin/*.cpp
    akonadiplugin/*.h
    akonadiplugin/*.c
    core/*.cpp
    core/*.h
    core/*.c
    debug/*.cpp
    debug/*.h
    debug/*.c
    lib/*.cpp
    lib/*.h
    lib/*.c
    runner/*.cpp
    runner/*.h
    runner/*.c
    search/*.cpp
    search/*.h
    search/*.c
    xapian/*.cpp
    xapian/*.h
    xapian/*.c
)

set(PRE_COMMIT_INSTALLED FALSE)
if(${CMAKE_SOURCE_DIR} STREQUAL ${PROJECT_SOURCE_DIR})
    if(EXISTS "${PROJECT_SOURCE_DIR}/.git/hooks/pre-commit")
        file(READ "${PROJECT_SOURCE_DIR}/.git/hooks/pre-commit" FILE_CONTENTS)
        string(FIND "${FILE_CONTENTS}" "File generated by pre-commit" INDEX)
        if(${INDEX} GREATER_EQUAL 0)
            set(PRE_COMMIT_INSTALLED TRUE)
        endif()
    endif()
endif()
if(NOT ${PRE_COMMIT_INSTALLED})
    find_program(PRE_COMMIT_EXECUTABLE pre-commit)
    if(PRE_COMMIT_EXECUTABLE)
        message(
            STATUS
            "This module has pre-commit support. pre-commit binary was found. Installing pre-commit hook"
        )
        execute_process(
            COMMAND "${PRE_COMMIT_EXECUTABLE}" install -f
            ERROR_QUIET
            RESULT_VARIABLE _precommitresult
            WORKING_DIRECTORY "${PROJECT_SOURCE_DIR}"
        )
        if(_precommitresult EQUAL 0)
            message(STATUS "Pre-commit installed.")
        else()
            message(
                WARNING
                "Error during installing pre-commit hook: ${PRE_COMMIT_EXECUTABLE} project source dir ${PROJECT_SOURCE_DIR}"
            )
            include(KDEGitCommitHooks)
            include(KDEClangFormat)
            kde_clang_format(${ALL_CLANG_FORMAT_SOURCE_FILES})
        endif()
    else()
        message(
            WARNING
            "This module has pre-commit support. pre-commit binary was not found."
        )
        include(KDEGitCommitHooks)
        include(KDEClangFormat)
        kde_clang_format(${ALL_CLANG_FORMAT_SOURCE_FILES})
    endif()
endif()

include(KDEInstallDirs)
include(KDECMakeSettings)
include(KDECompilerSettings NO_POLICY_SCOPE)
include(ECMInstallIcons)
include(ECMAddTests)
include(ECMQtDeclareLoggingCategory)
include(ECMDeprecationSettings)
include(ECMFeatureSummary)
include(ECMGenerateQDoc)

cmake_dependent_option(
    AKONADISEARCH_ENABLE_KRUNNER_RUNNER
    "Build KRunner runner to search contacts"
    ON
    "NOT WIN32;NOT APPLE"
    OFF
)

set(AKONADISEARCH_VERSION ${PIM_VERSION})

set(AKONADI_VERSION "6.6.40")
set(AKONADI_MIMELIB_VERSION "6.6.40")
set(KMIME_LIB_VERSION "6.6.44")
set(QT_REQUIRED_VERSION "6.9.0")
set(KPIM_MIME_VERSION "6.6.40")
set(KTEXTADDONS_MIN_VERSION "1.9.0")

find_package(Qt6 ${QT_REQUIRED_VERSION} CONFIG REQUIRED Core Test)
find_package(
    KF6
    ${KF_MIN_VERSION}
    REQUIRED
    COMPONENTS I18n Config Crash DBusAddons KCMUtils KIO
)
if(AKONADISEARCH_ENABLE_KRUNNER_RUNNER)
    find_package(KF6Runner ${KF_MIN_VERSION} REQUIRED)
endif()

find_package(Xapian CONFIG)
set_package_properties(
    Xapian
    PROPERTIES
        DESCRIPTION "The Xapian search engine library"
        URL "https://xapian.org"
        TYPE REQUIRED
)

find_package(KPim6Akonadi ${AKONADI_VERSION} CONFIG REQUIRED)
find_package(KF6Contacts ${KF_MIN_VERSION} CONFIG REQUIRED)
find_package(KPim6Mime ${KPIM_MIME_VERSION} CONFIG REQUIRED)
find_package(KPim6AkonadiMime ${AKONADI_MIMELIB_VERSION} CONFIG REQUIRED)
find_package(KF6CalendarCore ${KF_MIN_VERSION} CONFIG REQUIRED)
find_package(KF6TextUtils ${KTEXTADDONS_MIN_VERSION} CONFIG REQUIRED)

find_package(Corrosion CONFIG)
set_package_properties(
    Corrosion
    PROPERTIES
        PURPOSE "Required for safe parsing of html emails"
        DESCRIPTION
            "CMake scripts to seamlessly build and link to targets using cargo"
        URL https://github.com/corrosion-rs/corrosion
)

ecm_setup_version(PROJECT
    VARIABLE_PREFIX AKONADISEARCH
    VERSION_HEADER "${CMAKE_CURRENT_BINARY_DIR}/akonadi_search_version.h"
    PACKAGE_VERSION_FILE "${CMAKE_CURRENT_BINARY_DIR}/KPim6AkonadiSearchConfigVersion.cmake"
    SOVERSION 6
)
option(KDEPIM_RUN_AKONADI_TEST "Enable autotest based on Akonadi." TRUE)

# Compiler flags
add_definitions(-DTRANSLATION_DOMAIN=\"akonadi_search\")
add_definitions(-DQT_NO_CONTEXTLESS_CONNECT)
if(CMAKE_CXX_COMPILER_ID MATCHES "Clang")
    # Not setting for GNU due to too many warnings related to private members of base classes or around lambdas
    # see e.g. https://gcc.gnu.org/bugzilla/show_bug.cgi?id=56556 or https://gcc.gnu.org/bugzilla/show_bug.cgi?id=79328
    string(APPEND CMAKE_CXX_FLAGS " -Wshadow")
endif()
ecm_set_disabled_deprecation_versions(QT 6.11.0 KF 6.21.0 KMIME 6.7.0)

option(
    USE_UNITY_CMAKE_SUPPORT
    "Use UNITY cmake support (speedup compile time)"
    OFF
)

set(COMPILE_WITH_UNITY_CMAKE_SUPPORT OFF)
if(USE_UNITY_CMAKE_SUPPORT)
    set(COMPILE_WITH_UNITY_CMAKE_SUPPORT ON)
    add_definitions(-DCOMPILE_WITH_UNITY_CMAKE_SUPPORT)
endif()

# Turn exceptions on
kde_enable_exceptions()

include_directories(
    ${XAPIAN_INCLUDE_DIR}
    ${CMAKE_SOURCE_DIR}
    ${CMAKE_SOURCE_DIR}/core
    ${CMAKE_BINARY_DIR}/core
    ${CMAKE_SOURCE_DIR}/xapian
    ${CMAKE_BINARY_DIR}/xapian
)
# Config files
set(CMAKECONFIG_INSTALL_DIR "${KDE_INSTALL_CMAKEPACKAGEDIR}/KPim6AkonadiSearch")
# Targets
add_subdirectory(xapian)
add_subdirectory(core)
add_subdirectory(agent)
add_subdirectory(lib)
add_subdirectory(akonadiplugin)
add_subdirectory(search)
add_subdirectory(debug)
if(AKONADISEARCH_ENABLE_KRUNNER_RUNNER)
    add_subdirectory(runner)
endif()

configure_package_config_file(
    "${CMAKE_CURRENT_SOURCE_DIR}/KPimAkonadiSearchConfig.cmake.in"
    "${CMAKE_CURRENT_BINARY_DIR}/KPim6AkonadiSearchConfig.cmake"
    INSTALL_DESTINATION ${CMAKECONFIG_INSTALL_DIR}
)

install(
    FILES
        "${CMAKE_CURRENT_BINARY_DIR}/KPim6AkonadiSearchConfig.cmake"
        "${CMAKE_CURRENT_BINARY_DIR}/KPim6AkonadiSearchConfigVersion.cmake"
    DESTINATION ${CMAKECONFIG_INSTALL_DIR}
    COMPONENT devel
)

install(
    EXPORT KPim6AkonadiSearchTargets
    NAMESPACE KPim6::
    DESTINATION "${CMAKECONFIG_INSTALL_DIR}"
    FILE KPim6AkonadiSearchTargets.cmake
)

install(
    FILES "${CMAKE_CURRENT_BINARY_DIR}/akonadi_search_version.h"
    DESTINATION ${KDE_INSTALL_INCLUDEDIR}/KPim6/AkonadiSearch
    COMPONENT Devel
)

ecm_qt_install_logging_categories(
        EXPORT AKONADISEARCH
        FILE akonadi-search.categories
        DESTINATION ${KDE_INSTALL_LOGGINGCATEGORIESDIR}
)

if(DEFINED kde_configure_git_pre_commit_hook)
    kde_configure_git_pre_commit_hook(CHECKS CLANG_FORMAT)
endif()
ki18n_install(po)
ecm_feature_summary(WHAT ALL FATAL_ON_MISSING_REQUIRED_PACKAGES)
